behavior TabContainer
	-- init handles the default tab selection.  If a document hash exists
	-- (and points to one of our tabs) then select it first.  Otherwise,
	-- select the first tab in the list
	init
		if window.location.hash is not "" then
			set target to the first <[aria-controls="${window.location.hash.slice(1)}"]/>
		end

		if not target then
			set target to first <[role=tab]/> in me
		end

		send selectTab to target
	end

	-- select highlighted tab on SPACE (expected for ARIA buttons)
	on keydown[keyCode==32]
		send selectTab to first <[role=tab]:focus/>

	-- select highlighted tab on ENTER (additional key to select tabs)
	on keydown[keyCode==13]
		send selectTab to first <[role=tab]:focus/>

	-- handle mouse clicks and touch events directly on tabs
	on pointerdown from <[role=tablist] [role=tab]/>
		send selectTab to target
	end

	on selectTab(target)
		for tab in <[role=tab] /> in me
			if tab is target
				add [@aria-selected="true"] to tab
			else
				remove [@aria-selected] from tab
			end
		end

		for panel in <[role=tabpanel] /> in me
		    set hide to (panel[@id] != target[@aria-controls])
			set { hidden: hide } on panel
			if not hide
				add .grid to panel
			else
			    remove .grid from panel
			end
		end
	end
end
